Lang {
  Recipe
    = "#" space* "Equipment" Equipment* "#" space* "Ingredients" Ingredient* "#" space* "Steps" Step*
  Equipment
    = QuantifiedNoun ("," Adjectives)? ("as" Noun)? "."
  Ingredient
    = QuantifiedNoun ("," Adjectives)? ("as" Noun)? "."
  Step = SubStep "."
  SubStep = Adverbs? Verb StepNouns ("with" QuantifiedNouns)? ("at" Level)? (Locator Noun)? ("for" Duration)? ("until" StepPredicate)? ("and" SubStep)?
  StepPredicate
    = "temperature" spaces "is" Temperature -- temperature
    | (Noun spaces "is") ? Adjectives -- adjective
  StepNouns
    = "it"
    | "everything"
    | QuantifiedNouns

  QuantifiedNouns = QuantifiedNoun ("," QuantifiedNoun)*
  QuantifiedNoun
    = Quantity "-" Quantity Noun -- range
    | Quantity Noun -- quantified
    | Noun -- noun
  Quantity
    = "all" -- all
    | Number? "pinch" -- pinch
    | "few" (spaces UnitNoun)? -- few
    | Number UnitNoun -- measured
    | Integer "x" -- parts
    | Number -- numbered

  Locator = "in" | "on" | "over" | "to"
  Level = "low" | "medium-low" | "medium" |  "medium-high" | "high" | "max"
  Temperature = Decimal TempUnit
  Duration = Number UnitNoun ("-" Number UnitNoun)?
  TempUnit = "F" | "C" | "K"

  Number
    = Fraction
    | Decimal 
  Fraction = Integer "/" Integer
  Decimal = Integer ("." Integer) ?
  Integer = digit+

  Adjectives = Adjective ("," Adjective)*
  Adverbs = Adverb ("," Adverb)*
  Noun = pascalCase
  Verb = pascalCase
  UnitNoun = camelCase
  Adjective = camelCase
  Adverb = camelCase
  pascalCase = upper letter*
  camelCase = lower letter*
}
